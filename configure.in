dnl $Id$

AC_INIT(global.ent)

PHP_BUILD_DATE=`date '+%Y-%m-%d'`
AC_SUBST(PHP_BUILD_DATE)

dnl used by checkdoc
AC_PATH_PROG(PERL_PATH, perl)

AC_MSG_CHECKING(for DocBook doctype)
check_catalog_files() {
   for catalog in $@; do
      DBDT=`grep -E '^PUBLIC.*"docbook.dtd".*$' $catalog 2>/dev/null | cut -d'"' -f 2 `
      if test -n "$DBDT" ; then
          DOCBOOK_DOCTYPE=$DBDT
          return 1
      fi  
      MORECATS=`grep -i '^catalog' $catalog 2>/dev/null | cut -d'"' -f 2`
      if test -n "$MORECATS" ; then
         if check_catalog_files $MORECATS ; then
           return 1
         else 
           return 0
         fi
      fi
      return 0
   done
   return 0
}
if test -n "$SGML_CATALOG_FILES" ; then
   CATFILES=`echo $SGML_CATALOG_FILES | sed -e 's/:/ /'g`
   check_catalog_files $CATFILES
fi
if test -n "$DOCBOOK_DOCTYPE"; then
   AC_MSG_RESULT(got $DOCBOOK_DOCTYPE)
else 
   DOCBOOK_DOCTYPE="-//Davenport//DTD DocBook V3.0//EN"
   AC_MSG_RESULT(defaulting to $DOCBOOK_DOCTYPE)
fi 
AC_SUBST(DOCBOOK_DOCTYPE)

AC_MSG_CHECKING(for docbook.dsl)
for dir in /usr/lib/sgml/stylesheet/dsssl/docbook/nwalsh \
       /usr/lib/dsssl/stylesheets/docbook \
       /usr/lib/dsssl/stylesheets/nwalsh-modular \
       /usr/lib/dsssl/stylesheets/docbook \
       /usr/lib/sgml/stylesheets/nwalsh-modular \
       /usr/lib/sgml/docbook\
       /opt/sgml/lib/stylesheets/docbook \
       /usr/share/sgml/docbk30/dsl \
       /usr/share/sgml/docbkdsl \
       /usr/local/lib/dsssl/stylesheets/docbook \
       /usr/local/lib/dsssl/stylesheets/nwalsh-modular \
       /usr/local/lib/sgml/stylesheets/docbook \
       /usr/local/lib/sgml/stylesheets/nwalsh-modular \
       /usr/local/lib/sgml/docbook ; do
    if test -d "$dir"; then
    DOCBOOK_HTML="$dir/html/docbook.dsl"
    DOCBOOK_PRINT="$dir/print/docbook.dsl"
    AC_MSG_RESULT(in $dir)
    break
    fi
done
if test -z "$DOCBOOK_HTML"; then
    DOCBOOK_HTML="/usr/lib/sgml/stylesheets/nwalsh-modular/html/docbook.dsl"
    DOCBOOK_PRINT="/usr/lib/sgml/stylesheets/nwalsh-modular/print/docbook.dsl"
    AC_MSG_RESULT(defaulting)
fi
AC_SUBST(DOCBOOK_HTML)
AC_SUBST(DOCBOOK_PRINT)

AC_MSG_CHECKING(for PHP source path)
AC_ARG_WITH(source,
[  --with-source=[DIR]     Look at the specified source directory],
[
    if test "$withval" = "no"; then
	PHP_SOURCE=$withval
    else
	PHP_SOURCE=../php3
    fi
],[
  PHP_SOURCE=../php3
])
AC_MSG_RESULT($PHP_SOURCE)
AC_SUBST(PHP_SOURCE)

AC_MSG_CHECKING(for language)
AC_ARG_WITH(lang,
[  --with-lang=LANG        Choose a language to work with],
[
    if test "$withval" = "yes"; then
	LANG=en
	AC_MSG_RESULT([en (default)])
    else
        if test ! -d "$withval"; then
	    AC_MSG_RESULT()
	    AC_MSG_ERROR(Language \"$withval\" not supported!)
	fi
	LANG=$withval
    fi
],[
    LANG=en
    AC_MSG_RESULT([en (default)])
])
AC_SUBST(LANG)

AC_OUTPUT(Makefile version.ent checkdoc manual.sgml howto.sgml common.dsl howto.dsl html.dsl print.dsl phpweb.dsl php.dsl)
chmod +x checkdoc
